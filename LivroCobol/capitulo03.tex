%------------------------------------------------------------------------------------
%	CHAPTER 2
%------------------------------------------------------------------------------------
\chapterimage{headMontagem.png}
\chapter{Cobol como Banco de Dados}

\begin{remark}
	"As pessoas têm uma enorme tendência a resistir à mudança. Adoram dizer: ‘Sempre fizemos assim.’ Eu tento lutar contra isso." (\textit{Grace Hopper}, pioneira da computação e criadora do COBOL)
\end{remark}

\section{Trabalhar com Dados}\index{Cobol como Banco de Dados}
Obviamente todos sabem que o forte do Cobol é sua capacidade de processar dados, e são a razão da existência do Cobol até os dias atuais. Nenhum outro banco de dados (seja SQL ou NoSQL) possui a capacidade de um sistema em Cobol, pergunte a qualquer instituição financeira se eles trocariam o Cobol por outro sistema (mesmo nos dias atuais com a dificuldade em se encontrar quem forneça manutenção para tais sistemas).

Vamos começar com leitura de dados simples através de um arquivo sequencial, o detalhe mais importante aqui é que o Cobol faz isso através das leituras das POSIÇÕES dos dados, por exemplo o seguinte arquivo:

\begin{lstlisting}[]
12321Joao                Silva               M
13434Maria               Silva               F
43543Luiza               Albuquerque         F
53453Mario               Oliveira            M
43454Samara              Correia             F
87978Alberico            Soares              M
87886Carlos              Souza               M
76535Joao                Ramalho             M
54543Roberta             Martins             F
\end{lstlisting}

Observamos que as colunas de 1 a 5 contém a matrícula do funcionários, as 20 próximas colunas (6 a 25) seu nome, as próximas 20 (26 a 35) seu último nome e o carácter 46 contém o gênero. E é dessa forma que devemos informar ao Cobol de modo que a leitura seja informada corretamente.

\begin{note}[Notação dos códigos]
	Apenas para facilitar a leitura a partir desse programa iremos suprimir os números das linhas e o espaçamento inicial, considere que o código do programa SEMPRE deve começar a partir da coluna 7.
\end{note}

Dessa vez vamos por partes no programa que leia os valores contidos neste arquivo e mostre na saída cada um deles, ao término quantos homens (último campo como "M") e quantas mulheres (último campo como "F") existem na empresa. Vamos ver este programa divisão a divisão.

Na \textbf{IDENTIFICATION} não existe muitas modificações:
\begin{lstlisting}[]
IDENTIFICATION DIVISION.
    PROGRAM-ID. ContagemFuncionarios.
    AUTHOR. Fernando Anselmo.	
\end{lstlisting}

Como sempre colocamos o nome do programa na \textbf{PROGRAM-ID} e o autor deste na \textbf{AUTHOR}. O nome do programa é o único parâmetro obrigatório os outros totalmente opcionais (tanto que até o momento além do \textbf{AUTHOR} não foram usados), para conhecimento, são eles:
\begin{itemize}
	\item \textbf{INSTALLATION}: Indica o nome da instalação ou organização onde o programa será executado.
	\item \textbf{DATE-WRITTEN}: Especifica a data em que o programa foi escrito.
	\item \textbf{DATE-COMPILED}: Indica a data em que o programa foi compilado.
	\item \textbf{SECURITY}: Fornece informações de segurança relacionadas ao programa.
	\item \textbf{REMARKS}: Permite adicionar comentários ou notas livres sobre o programa.
\end{itemize}

São meramente descritivos e não possuem qualquer impacto na execução do programa, sendo apenas informativos. Por exemplo, aqui está uma \textbf{IDENTIFICATION DIVISION} completa:
\begin{lstlisting}[]
IDENTIFICATION DIVISION.
    PROGRAM-ID. ContagemFuncionarios.
    AUTHOR. Fernando Anselmo.
    INSTALLATION. Empresa Decus in Labore.
    DATE-WRITTEN. 12-JAN-2025.
    DATE-COMPILED. 16-JAN-2025.
    SECURITY. Apenas para uso interno.
    REMARKS. Programa para contagem da equipe de instrutores.
\end{lstlisting}

A \textbf{ENVIRONMENT} começa a mudar um pouco, pois precisamos indicar o arquivo:
\begin{lstlisting}[]
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT FUNCIONARIOS ASSIGN TO "FUNCIONARIOS.DATA"
        ORGANIZATION IS LINE SEQUENTIAL.	
\end{lstlisting}

Esta divisão descreve o ambiente de execução do programa, incluindo os dispositivos de entrada e saída, como arquivos, impressoras e terminais. Assim, criamos a \textbf{INPUT-OUTPUT SECTION} que especifica os arquivos que serão manipulados pelo programa. Nessa temos a \textbf{FILE-CONTROL} para informar como associar os arquivos lógicos do COBOL (usados no código) aos arquivos físicos (armazenados no sistema de arquivos).

\textbf{SELECT} no Cobol é diferente do SQL, aqui associa o nome lógico do arquivo no programa (FUNCIONARIOS) ao nome físico no sistema de arquivos. E a cláusula \textbf{ASSIGN TO} define o nome físico do arquivo, ou seja, seu nome real no sistema operacional.

\textbf{ORGANIZATION} mostra como os dados no arquivo estão organizados. E finalmente \textbf{LINE SEQUENTIAL} significa que o arquivo é baseado em linhas, como um arquivo de texto comum. Cada registro no arquivo termina com um caractere de nova linha (\\n) ou sequência equivalente no sistema.

\begin{lstlisting}[]
DATA DIVISION.
FILE SECTION.
FD FUNCIONARIOS.
01 DETALHEFUNCIONARIO.
   88 FINALREGISTRO VALUE HIGH-VALUES.
   05 MATRICULA-FUNCIONARIO   PIC 9(5).
   05 NOME-FUNCIONARIO.
       10 PRIMEIRO-NOME       PIC X(20).
       10 ULTIMO-NOME         PIC X(20).
   05 GENERO                  PIC X(1).

WORKING-STORAGE SECTION.
01 CONTADORES.
   05 TOTAL-HOMENS            PIC 9(3) VALUE 0.
   05 TOTAL-MULHERES          PIC 9(3) VALUE 0.

01 LEITURA-FINALIZADA          PIC X VALUE "N".	
\end{lstlisting}

Agora temos a \textbf{FILE SECTION} que define a estrutura do arquivo que será lido, o nome desse deve ser exatamente o descrito na cláusula \textbf{SELECT}. O detalhe que vale atenção é a existencia do registro 88 que é definido para indicar o fim dos registros no arquivo ou quando não há mais dados válidos. Utiliza o valor especial \textbf{HIGH-VALUES}, que representa o valor mais alto possível para o conjunto de caracteres em uma tabela de códigos, como \textbf{EBCDIC} ou \textbf{ASCII}.

Em seguida montamos a estrutura completa como o Cobol armazena cada linha que será lida. A \textbf{WORKING-STORAGE SECTION} contém a definição dos contadores que usaremos para definir o total de homens e mulheres. E uma chave que usamos para indicar que terminamos a leitura do programa.

E por fim a \textbf{PROCEDURE}:
\begin{lstlisting}[]
PROCEDURE DIVISION.
INICIO.
    DISPLAY "===============================".
    DISPLAY " Contagem de Funcionários".
    DISPLAY "===============================".

    OPEN INPUT FUNCIONARIOS.

    PERFORM UNTIL LEITURA-FINALIZADA = "S"
        READ FUNCIONARIOS INTO DETALHEFUNCIONARIO
            AT END
                MOVE "S" TO LEITURA-FINALIZADA
            NOT AT END
                INSPECT PRIMEIRO-NOME REPLACING ALL " " 
                    BY LOW-VALUES
                INSPECT ULTIMO-NOME REPLACING ALL " " 
                    BY LOW-VALUES
                DISPLAY MATRICULA-FUNCIONARIO " " GENERO " "
                   PRIMEIRO-NOME " " ULTIMO-NOME
                IF GENERO = "M"
                    ADD 1 TO TOTAL-HOMENS
                ELSE
                    IF GENERO = "F"
                        ADD 1 TO TOTAL-MULHERES
                    END-IF
                END-IF
        END-READ
    END-PERFORM. 

    CLOSE FUNCIONARIOS.

    DISPLAY "=============================".
    DISPLAY "Resumo:".
    DISPLAY " Total de Homens..: " TOTAL-HOMENS.
    DISPLAY " Total de Mulheres: " TOTAL-MULHERES.
    DISPLAY "=============================".

    STOP RUN.	
\end{lstlisting}

Pode parecer bem estranha essa estrutura mas vamos simplificar um pouco, o comando \textbf{OPEN INPUT} abre o arquivo para leitura e em seguida temos um laço \textbf{PERFORM UNTIL}, que possui a seguinte estrutura simplificada:
\begin{lstlisting}[]
READ FUNCIONARIOS INTO DETALHEFUNCIONARIO
   AT END
       MOVE "S" TO LEITURA-FINALIZADA
   NOT AT END
       DISPLAY "Registro válido."
END-READ
\end{lstlisting}

Quando o arquivo alcança o fim ou quando um registro vazio (sem dados válidos) é lido, o COBOL pode preencher aquele campo do registro \textbf{HIGH-VALUES}. Temos dois comandos básicos \textbf{AT END} quando o fim for atingido e \textbf{NOT AT END} enquanto não for.

O detalhe mais importante aqui é a falta do "." (ponto final) nas instruções, toda vez que estamos dentro de um comando NÃO usamos mais o ponto final para indicar o fim do comando, sendo assim esse só será usado no \textbf{END-PERFORM}.

A instrução \textbf{INSPECT [VARIÁVEL] REPLACING ALL " " BY LOW-VALUES} retira qualquer espaço em branco, assim temos que ter todo cuidado ao usá-lo, pois se tivéssemos um valor "  Meu Nome é Fernando", o resultado seria: "MeuNomeéFernando", ou seja, não pense nisso como um equivalente ao método \textbf{TRIM()} de outras linguagens. Esse é um dos motivos de até nos dias atuais em bancos modernos acostumarmos com a separação dos nomes em primeiro e último sem o uso de outros nomes intermediários, mesmo com as linguagens mais modernas.

O comando \textbf{IF} do Cobol, possui a cláusula opcional \textbf{ELSE} e deve ser terminado com \textbf{END-IF}, e no nosso caso é utilizado para totalizar o total de homens e mulheres, é óbvio que poderíamos simplificá-lo para:
\begin{lstlisting}[]
IF GENERO = "M"
    ADD 1 TO TOTAL-HOMENS
ELSE
    ADD 1 TO TOTAL-MULHERES
END-IF
\end{lstlisting}

Mas estaríamos arriscando que se houvesse um erro no registro e não contivesse o valor "M" ou "F" seria totalizado como uma mulher, e apenas para garantirmos a totalização correta adicionamos mais um \textbf{IF} para verificarmos se realmente o gênero contém o carácter "F".

Ao término da leitura dos dados fechamos o arquivo \textbf{CLOSE FUNCIONARIOS} e mostramos a totalização dos dados.

\section{Separação em \textit{Labels}}\index{Cobol como Banco de Dados}
O programa visto anteriormente realiza sua função corretamente, porém está estruturado de forma incorreta o ideal é sempre deixá-lo mais claro e organizado. Em COBOL, pode ser feito com a utilização de parágrafos (chamados de \textit{labels}).

Descrição dos parágrafos:
\begin{enumerate}
	\item \textbf{PROCESSAR-REGISTROS}: que controla o loop principal e chama as funções para leitura do arquivo, exibição dos dados e contagem de gênero.
	\item \textbf{LER-REGISTRO}: responsável por ler um registro do arquivo e determina se é o fim do arquivo (\textbf{AT END}).
	\item \textbf{EXIBIR-FUNCIONARIO}: para mostrar os dados do funcionário no console.
	\item \textbf{CONTAR-GENERO}: que incrementa os contadores de homens e mulheres com base no campo GENERO.
	\item \textbf{EXIBIR-RESUMO}: para mostrar o total de homens e mulheres ao final.
\end{enumerate}

Essa reestruturação sempre deve ser aplicada se pensarmos na \textbf{clareza} onde cada funcionalidade é separada em parágrafos específicos, para facilitar a leitura, ou \textbf{reutilização} onde os parágrafos podem ser chamados em diferentes partes do programa se necessário. Mas devemos sempre aplicar quando pensamos principalmente na \textbf{manutenção} do código, pois alterações em uma parte específica não afetam outras funcionalidades e são muito mais fáceis de serem manejadas.

O comando \textbf{PERFORM} é o responsável para a chamada de cada um desses \textit{labels}, o que acontece, o \textbf{label} é chamado e ao seu término retorna a execução para o ponto de chamada. Assim a \textbf{PROCEDURE DIVISION} vista no programa anterior seria assim redefinida:

\begin{lstlisting}[]
PROCEDURE DIVISION.
INICIO.
    DISPLAY "===============================".
    DISPLAY " Contagem de Funcionários".
    DISPLAY "===============================".

    OPEN INPUT FUNCIONARIOS.
    PERFORM PROCESSAR-REGISTROS.
    CLOSE FUNCIONARIOS.
    PERFORM EXIBIR-RESUMO.
    STOP RUN.

PROCESSAR-REGISTROS.
    PERFORM UNTIL LEITURA-FINALIZADA = "S"
       PERFORM LER-REGISTRO
       IF LEITURA-FINALIZADA NOT = "S"
          PERFORM EXIBIR-FUNCIONARIO
          PERFORM CONTAR-GENERO
       END-IF
    END-PERFORM.    

LER-REGISTRO. 
    READ FUNCIONARIOS INTO DETALHEFUNCIONARIO
        AT END
            MOVE "S" TO LEITURA-FINALIZADA
        NOT AT END
            CONTINUE
    END-READ.

EXIBIR-FUNCIONARIO.
    INSPECT PRIMEIRO-NOME REPLACING ALL " " 
        BY LOW-VALUES
    INSPECT ULTIMO-NOME REPLACING ALL " " 
        BY LOW-VALUES
    DISPLAY MATRICULA-FUNCIONARIO " " GENERO " "
       PRIMEIRO-NOME " " ULTIMO-NOME. 

CONTAR-GENERO.
    IF GENERO = "M"
        ADD 1 TO TOTAL-HOMENS
    ELSE
        IF GENERO = "F"
            ADD 1 TO TOTAL-MULHERES
        END-IF
    END-IF.

EXIBIR-RESUMO.
    DISPLAY "=============================".
    DISPLAY "Resumo:".
    DISPLAY " Total de Homens..: " TOTAL-HOMENS.
    DISPLAY " Total de Mulheres: " TOTAL-MULHERES.
    DISPLAY "=============================".
\end{lstlisting}

O resultado da execução é similar ao visto anteriormente, com a grande vantagem que se tivermos que modificar qualquer detalhe do programa saberemos exatamente aonde realizá-lo.

\section{Ler arquivos CSV}\index{Cobol como Banco de Dados}
Nativamente no Cobol não existe quaisquer referências a formatos de arquivos usados atualmente de transporte como \textbf{CSV}, \textbf{XML} ou \textbf{JSon}. Mas e se (\textit{what if?}) desejássemos ler tais arquivos? Como fazer? Seria possível criar um programa em Cobol para que obter dados de um arquivo \textbf{CSV} (por exemplo)?

A resposta para isso seria que devemos utilizar de nosso raciocínio lógico e pensar em uma solução, sabemos que em arquivos CSV os valores são separados por algum caractere (geralmente vírgulas), porém não é assim que o Cobol lê arquivos, usa elementos posicionais. Sendo assim podemos ler uma linha inteira e manipulá-la.

Por exemplo, considerando o seguinte arquivo CSV:
\begin{lstlisting}[]
Nome,Data Nascimento,Gênero
Maria Silva,1987,Feminino
João Silva,1845,Masculino
Carlos Silva,1956,Masculino
\end{lstlisting}

Devemos nos ater a alguns detalhes, primeiro o maior tamanho de uma linha (não ultrapassa o tamanho de 100 caracteres) e segundo quantos elementos possui esse arquivo (3 elementos). Agora podemos criar nosso programa:
\begin{lstlisting}[]
IDENTIFICATION DIVISION.
  PROGRAM-ID. LerCsv.
  AUTHOR. Fernando Anselmo.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
  SELECT ARQ-CSV ASSIGN TO "entrada.csv"
    ORGANIZATION IS LINE SEQUENTIAL
    ACCESS IS SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD ARQ-CSV.
01 REGISTRO-CSV         PIC X(100).

WORKING-STORAGE SECTION.
01 WS-FIM-DO-ARQUIVO    PIC X(01) VALUE 'N'.
01 WS-CONTADOR          PIC 9(05) VALUE ZEROS.
01 WS-REGISTRO          PIC 9(05) VALUE ZEROS.

01 WS-DETALHE.
  05 WS-DADO          PIC X(50) OCCURS 3 TIMES.

PROCEDURE DIVISION.
MAIN-PROCEDURE.
  OPEN INPUT ARQ-CSV
  PERFORM LER-CABECALHO
  PERFORM PROCESSAR-REGISTROS UNTIL WS-FIM-DO-ARQUIVO = 'S'
  CLOSE ARQ-CSV
  STOP RUN.

LER-CABECALHO.
  READ ARQ-CSV INTO REGISTRO-CSV
    NOT AT END
      PERFORM SEPARAR-CAMPOS THRU FIM-SEPARAR-CAMPOS
      DISPLAY "Cabeçalho:"
      PERFORM EXIBIR THRU FIM-EXIBIR
  END-READ.

SEPARAR-CAMPOS.
  MOVE SPACES TO WS-DETALHE
  UNSTRING REGISTRO-CSV DELIMITED BY ','
    INTO WS-DADO(1) WS-DADO(2) WS-DADO(3)
  END-UNSTRING.
FIM-SEPARAR-CAMPOS.
  EXIT.

PROCESSAR-REGISTROS.
  READ ARQ-CSV INTO REGISTRO-CSV
    AT END
      MOVE 'S' TO WS-FIM-DO-ARQUIVO
    NOT AT END
      ADD 1 TO WS-REGISTRO
      PERFORM SEPARAR-CAMPOS THRU FIM-SEPARAR-CAMPOS
      DISPLAY "Registro #" WS-REGISTRO ":"
      PERFORM EXIBIR THRU FIM-EXIBIR
  END-READ.

EXIBIR.
  PERFORM VARYING WS-CONTADOR FROM 1 BY 1 UNTIL WS-CONTADOR > 3
    DISPLAY "Dado " WS-CONTADOR ": " WS-DADO(WS-CONTADOR)
  END-PERFORM.
FIM-EXIBIR.
  EXIT.	
\end{lstlisting}

Ler o arquivo será exatamente o mesmo visto anteriormente, com a diferença que leremos uma linha completa, na variável \textbf{REGISTRO-CSV} com 100 caracteres, outro detalhe está em outra variável \textbf{WS-DADO} que ocorre 3 vezes (essa é uma definição de Cobol que indica um \textit{Array} - ou uma \textit{Lista}).

A função principal aqui é \textbf{SEPARAR-CAMPOS} que faz o trabalho com o comando \textbf{UNSTRING} de separar uma linha delimitado por um determinado caractere e colocar cada "pedaço" em um elemento de \textbf{WS-DADO}.

Outro ponto de interesse nesse programa está no método \textbf{EXIBIR} que temos novamente o comando \textbf{PERFORM} utilizado de um modo diferente do que já conhecemos, o formato é similar a um laço de repetição das linguagens atuais, como uma mescla entre comandos \textbf{WHILE} e \textbf{FOR}. Esse comando em GNU Cobol é um laço que percorre um intervalo de valores e executa um bloco de código repetidamente.

\codigo{PERFORM VARYING WS-CONTADOR FROM 1 BY 1 UNTIL WS-CONTADOR > 3}

Esse comando inicializa a variável \textbf{WS-CONTADOR} com o valor 1 (\textbf{FROM 1}). A cada passada, incrementa essa variável de 1 em 1 (\textbf{BY 1}). E continua o laço enquanto o valor for menor ou igual a 3 (\textbf{UNTIL WS-CONTADOR > 3}).

\section{Arquivos Relativos e Indexados}\index{Cobol como Banco de Dados}
Já sabemos e conhecemos como funciona o formato de arquivos sequenciais que são usados para ler textos e seus registros são lidos em ordem:
\begin{lstlisting}[]
SELECT ARQUIVO-TXT ASSIGN TO "clientes.txt"
  ORGANIZATION IS LINE SEQUENTIAL
  ACCESS MODE IS SEQUENTIAL.	
\end{lstlisting}

Mas existem outros formatos conhecidos como \textbf{Arquivos Relativos} (\textit{RELATIVE}), que permitem o acesso direto a um registro específico, com base em seu número relativo:
\begin{lstlisting}[]
SELECT ARQUIVO-RELATIVO ASSIGN TO "dados.dat"
  ORGANIZATION IS RELATIVE
  ACCESS MODE IS DYNAMIC
  RELATIVE KEY IS WS-CHAVE-RELATIVA.	
\end{lstlisting}

Neste caso \textbf{WS-CHAVE} representa a posição do registro no arquivo.

\textbf{Arquivos Indexados} (\textit{INDEXED}), que suportam acesso direto ou sequencial e são úteis para bancos de dados indexados:
\begin{lstlisting}[]
SELECT ARQUIVO-INDEXADO ASSIGN TO "dados.idx"
  ORGANIZATION IS INDEXED
  ACCESS MODE IS DYNAMIC
  RECORD KEY IS WS-ID-CLIENTE
  ALTERNATE RECORD KEY IS WS-NOME-CLIENTE WITH DUPLICATES.	
\end{lstlisting}

Neste caso, permite pesquisar registros pelo ID (WS-ID-CLIENTE) ou pelo nome (WS-NOME-CLIENTE), sendo que nomes podem se repetir (WITH DUPLICATES).

\section{Gravar e Ler em Arquivos Relativos}\index{Cobol como Banco de Dados}
Vamos começar com arquivos relativos, criaremos um programa para cadastrar e ler uma tabela contendo dados básicos de alunos, tais como, matrícula, nome e idade.

As duas primeiras divisões são assim construídas:
\begin{lstlisting}[]
IDENTIFICATION DIVISION.
  PROGRAM-ID. ALUNO-RELATIVO.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
  SELECT ARQUIVO-ALUNO ASSIGN TO "alunos.dat"
    ORGANIZATION IS RELATIVE
    ACCESS MODE IS DYNAMIC
    RELATIVE KEY IS WS-CHAVE-RELATIVA.	
\end{lstlisting}

Este arquivo \textbf{alunos.dat} deve estar anteriormente criado, apenas um simples arquivo vazio (podemos usar um editor qualquer para realizar esse procedimento). No Linux com o comando: \\
\codigo{\$ touch alunos.dat}

Quando este arquivo é aberto, o GNU Cobol irá transformá-lo para um formato binário que não deve mais ser lido por um editor comum.

\begin{lstlisting}[]
DATA DIVISION.
FILE SECTION.
FD ARQUIVO-ALUNO.
01 REGISTRO-ALUNO.
  05 WS-MATRICULA      PIC 9(3).  
  05 WS-NOME           PIC X(30). 
  05 WS-IDADE          PIC 9(2).  

WORKING-STORAGE SECTION.
01 WS-OPCAO             PIC 9.
01 WS-CHAVE-RELATIVA    PIC 9(3).
01 WS-CHAVE-BUSCA       PIC 9(3).	
\end{lstlisting}

Próximo passo e estruturarmos o arquivo e definir as variáveis que iremos utilizar, \textbf{WS-CHAVE-RELATIVA} é uma associação da chave do arquivo.

Agora na \textbf{PROCEDURE DIVISION}, inicialmente temos:
\begin{lstlisting}[]
PROCEDURE DIVISION.
INICIO.
  DISPLAY "--------------------".
  DISPLAY "Sistema de Alunos".
  DISPLAY "--------------------".
  DISPLAY "1 - Inserir Aluno".
  DISPLAY "2 - Buscar Aluno".
  DISPLAY "3 - Sair".
  ACCEPT WS-OPCAO.

  EVALUATE WS-OPCAO
    WHEN 1
      PERFORM INSERIR-ALUNO
    WHEN 2
      PERFORM BUSCAR-ALUNO
    WHEN 3
      STOP RUN
    WHEN OTHER
      DISPLAY "Opção Inválida!" 
      PERFORM INICIO
  END-EVALUATE.	
\end{lstlisting}

Neste ponto construímos o Menu de Opções no qual podemos escolher uma criar um novo aluno ou buscar um determinado aluno. O comando \textbf{EVALUATE} é similar a um comando ESCOLHA (\textit{switch}) atual.

\begin{lstlisting}[]
INSERIR-ALUNO.
  DISPLAY "--------------------".
  DISPLAY "Cadastrar Aluno".
  DISPLAY "--------------------".
  DISPLAY "Matrícula do Aluno: ".
  ACCEPT WS-CHAVE-RELATIVA.
  MOVE WS-CHAVE-RELATIVA TO WS-MATRICULA.
  DISPLAY "Nome do Aluno: ".
  ACCEPT WS-NOME.
  DISPLAY "Idade do Aluno: ".
  ACCEPT WS-IDADE.

  OPEN I-O ARQUIVO-ALUNO.
  WRITE REGISTRO-ALUNO INVALID KEY
  DISPLAY "Erro ao gravar o registro!".
  CLOSE ARQUIVO-ALUNO.

  DISPLAY "Aluno gravado com sucesso!".
  PERFORM INICIO.	
\end{lstlisting}

Na fase de incluir um aluno solicitamos os dados deste, observa-se que temos que realizar um movimento entre a \textbf{WS-CHAVE-RELATIVA} para \textbf{WS-MATRICULA} de modo que o registro seja preenchido corretamente.

Outro detalhe interessante, que é adotado atualmente em sistemas modernos é: Abrir o arquivo, realizar uma operação e fechá-lo. Isso garante que o arquivo não permaneça aberto que pode gerar problemas de performance, ou mesmo excesso de usuários.

\begin{lstlisting}[]
BUSCAR-ALUNO.
  DISPLAY "--------------------".
  DISPLAY "Buscar Aluno".
  DISPLAY "--------------------".
  DISPLAY "Qual matrícula deseja obter? ".
  ACCEPT WS-CHAVE-BUSCA.

  OPEN INPUT ARQUIVO-ALUNO.
  MOVE WS-CHAVE-BUSCA TO WS-CHAVE-RELATIVA.
  READ ARQUIVO-ALUNO INVALID KEY
    DISPLAY "Registro não encontrado!"
  NOT INVALID KEY
    DISPLAY "--------------------"
    DISPLAY "Aluno Encontrado:"
    DISPLAY "Matrícula: " WS-MATRICULA
    DISPLAY "Nome: " WS-NOME
    DISPLAY "Idade: " WS-IDADE
  END-READ.
  CLOSE ARQUIVO-ALUNO.

  PERFORM INICIO.	
\end{lstlisting}

Para trazer o aluno, basta abrir o arquivo, obter o registro através da chave digitada, mostrar os dados e fechar o arquivo.

\section{Arquivos Indexados}\index{Cobol como Banco de Dados}

Agora vamos criar um \textbf{CRUD} \footnote{CRUD é um acrônimo para as quatro operações básicas que podem ser realizadas em um banco de dados ou arquivo: \textbf{C}reate (Criar), adicionar novos registros; \textbf{R}ead (Ler), Consultar ou visualizar registros existentes; \textbf{U}pdate (Atualizar), modificar um registro já existente; \textbf{D}elete (Excluir), remover um registro do sistema.} completo com a utilização desse modo.

As duas primeiras divisões são assim construídas:
\begin{lstlisting}[]
IDENTIFICATION DIVISION.
 PROGRAM-ID. GerenciadorProfessor.
 AUTHOR. Fernando Anselmo.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
 SELECT ARQ-PROFESSOR ASSIGN TO "professores.idx"
  ORGANIZATION IS INDEXED
  ACCESS MODE IS DYNAMIC
  RECORD KEY IS PRO-MATRICULA
  FILE STATUS IS WS-STATUS.
\end{lstlisting}

Este arquivo \textbf{professores.idx} será criado automaticamente quando iniciarmos e não é necessário criá-lo. o parâmetro \textbf{RECORD KEY} é relativo a chave primária da tabela e \textbf{FILE STATUS} os possíveis status do arquivo que são:

\begin{itemize}
	\item \textbf{"00"} - Operação bem-sucedida: Indica que tudo foi realizado com sucesso (como uma leitura ou gravação bem-sucedida).
	\item \textbf{"10"} - Final do arquivo (\textit{EOF}): Quando o programa tenta ler além do último registro ou a posição no arquivo está além do ponto final, esse código de status é retornado.
	\item \textbf{"20"} - Chave não encontrada (\textit{Key Not Found}): O comando START não conseguiu encontrar um registro correspondente à chave fornecida. Esse código é comumente retornado ao tentar posicionar-se em um arquivo com uma chave que não existe.
	\item \textbf{"21"} - Chave duplicada (\textit{Duplicate Key}): Tentativa de inserir um registro com uma chave que já existe no arquivo indexado. Esse erro é retornado ao tentar inserir uma chave duplicada.
	\item \textbf{"22"} - Chave inválida (\textit{Invalid Key}): A chave fornecida não é válida ou não pode ser usada no arquivo (isso pode ocorrer quando a chave não é compatível com o tipo ou tamanho da chave primária definida no arquivo).
	\item \textbf{"30"} - Erro interno: Um erro no sistema de arquivos ocorreu.
\end{itemize}

Vamos para a próxima divisão:
\begin{lstlisting}[]
DATA DIVISION.
FILE SECTION.
FD ARQ-PROFESSOR.
01 REG-PROFESSOR.
 05 PRO-MATRICULA  PIC X(8).
 05 PRO-NOME       PIC X(30).

WORKING-STORAGE SECTION.
01 WS-OPCAO        PIC 9.
01 WS-STATUS       PIC X(2).

01 WS-PROFESSOR.
 05 WS-MATRICULA   PIC X(8).
 05 WS-NOME        PIC X(30).
\end{lstlisting}

Estruturamos nosso arquivo, associando-o ao visto na \textbf{ENVIRONMENT}, criamos a chave \textbf{WS-STATUS} para analizarmos os status devolvidos e \textbf{WS-OPCAO} para controlar o menu. Por fim a mesma definição do arquivo para manipulação.

Agora na \textbf{PROCEDURE DIVISION}, inicialmente temos:
\begin{lstlisting}[]
\end{lstlisting}

Neste ponto construímos o Menu de Opções no qual podemos escolher uma criar um novo aluno ou buscar um determinado aluno. O comando \textbf{EVALUATE} é similar a um comando ESCOLHA (\textit{switch}) atual.

\begin{lstlisting}[]
PROCEDURE DIVISION.
INICIO.
 DISPLAY "--------------------------------".
 DISPLAY "Menu de Professor".
 DISPLAY "--------------------------------".
 DISPLAY " 1 - Cadastrar Professor".
 DISPLAY " 2 - Mostrar Professores".
 DISPLAY " 3 - Modificar Professor".
 DISPLAY " 4 - Excluir Professor".
 DISPLAY " 5 - Sair".
 DISPLAY "--------------------------------".
 DISPLAY "Escolha uma opção: ".
 ACCEPT WS-OPCAO.

 EVALUATE WS-OPCAO
  WHEN 1 
   PERFORM CADASTRAR
  WHEN 2 
   PERFORM MOSTRAR
  WHEN 3 
   PERFORM MODIFICAR
  WHEN 4 
   PERFORM EXCLUIR
  WHEN 5
   STOP RUN
  WHEN OTHER
   DISPLAY "Opção inválida!"
   PERFORM INICIO
 END-EVALUATE. 
\end{lstlisting}

Conforme visto no programa da seção anterior, criamos nosso menu para realizar as ações, porém agora precisamos de dois \textit{labels} para realizarmos abrir e fechar o arquivo:
\begin{lstlisting}[]
ABRIR-ARQUIVO.
 OPEN I-O ARQ-PROFESSOR.
 IF WS-STATUS = "35"
  OPEN OUTPUT ARQ-PROFESSOR
  CLOSE ARQ-PROFESSOR
  OPEN I-O ARQ-PROFESSOR.

FECHAR-ARQUIVO.
 CLOSE ARQ-PROFESSOR.	
\end{lstlisting}

Como dito anteriormente, para cada ação do nosso \textbf{CRUD} abriremos o arquivo, realizamos as ações necessárias e o fechamos.

Vamos para a primeira ação C:
\begin{lstlisting}[]
CADASTRAR.
 DISPLAY "--------------------------------".
 DISPLAY "Criar Professor".
 DISPLAY "--------------------------------".
 DISPLAY "Matrícula: ".
 ACCEPT WS-MATRICULA.
 DISPLAY "Nome: ".
 ACCEPT WS-NOME.

 MOVE WS-MATRICULA TO PRO-MATRICULA.
 MOVE WS-NOME TO PRO-NOME.

 PERFORM ABRIR-ARQUIVO.
 WRITE REG-PROFESSOR
  INVALID KEY DISPLAY "Erro: Matrícula já existe!".
 PERFORM FECHAR-ARQUIVO.
 PERFORM INICIO.	
\end{lstlisting}

Para inserir um professor, obtemos os dados deste, abrimos o arquivo, gravamos o registro, fechamos o arquivo e retornamos para o \textbf{INICIO}.

Vamos para a ação R:
\begin{lstlisting}[]
MOSTRAR.
 DISPLAY "--------------------------------".
 DISPLAY "Listar Professor".
 DISPLAY "--------------------------------".

 PERFORM ABRIR-ARQUIVO.
 MOVE "0" TO WS-STATUS.

 PERFORM UNTIL WS-STATUS = "10"
  READ ARQ-PROFESSOR NEXT RECORD
   AT END 
    MOVE "10" TO WS-STATUS
   NOT AT END
    DISPLAY "Matrícula: " PRO-MATRICULA
    DISPLAY "Nome: " PRO-NOME
    DISPLAY "--------------------------------"
  END-READ
 END-PERFORM.

 PERFORM FECHAR-ARQUIVO.
 PERFORM INICIO.	
\end{lstlisting}

Pode-se dizer que aqui estão as ações mais complexas desse programa, acertamos o \textbf{WS-STATUS} para "0" e enquanto este não chegar ao valor "10", lemos o próximo registro e o mostramos na tela.

Vamos para a ação U:
\begin{lstlisting}[]
MODIFICAR.
 DISPLAY "--------------------------------".
 DISPLAY "Modificar Professor".
 DISPLAY "--------------------------------".
 DISPLAY "Informe a matrícula do professor: ".
 ACCEPT WS-MATRICULA.

 PERFORM ABRIR-ARQUIVO.
 MOVE WS-MATRICULA TO PRO-MATRICULA.
 READ ARQ-PROFESSOR KEY IS PRO-MATRICULA
  INVALID KEY 
   DISPLAY "Matrícula não encontrada!"
  NOT INVALID KEY
   DISPLAY "Novo Nome: "
   ACCEPT WS-NOME
   MOVE WS-NOME TO PRO-NOME
   REWRITE REG-PROFESSOR
    DISPLAY "Registro atualizado!".
 PERFORM FECHAR-ARQUIVO.
 PERFORM INICIO.	
\end{lstlisting}

A modificação depende primeiro de encontrar o registro correto na tabela, uma vez encontrado modificamos os campos que não são chaves (uma chave primária NUNCA deve ser modificada) e reescrevemos o registro na tabela.

E por fim a ação D:
\begin{lstlisting}[]
EXCLUIR.
 DISPLAY "--------------------------------".
 DISPLAY "Eliminar Professor".
 DISPLAY "--------------------------------".
 DISPLAY "Informe a matrícula do professor: ".
 ACCEPT WS-MATRICULA.

 PERFORM ABRIR-ARQUIVO.
 MOVE WS-MATRICULA TO PRO-MATRICULA.
 READ ARQ-PROFESSOR KEY IS PRO-MATRICULA
  INVALID KEY 
   DISPLAY "Matrícula não encontrada!"
  NOT INVALID KEY
   DELETE ARQ-PROFESSOR
   DISPLAY "Professor removido!".
 PERFORM FECHAR-ARQUIVO.
 PERFORM INICIO.
\end{lstlisting}

Assim como na modificação, localizamos o registro pela chave e uma vez encontrado eliminamos na tabela.

\section{Diferenças entre Arquivos Relativos e Indexados}\index{Cobol como Banco de Dados}
Mas como podemos escolher entre arquivos relativos e indexados no COBOL, basicamente está na forma como os registros são acessados e organizados.

\subsection{Arquivos Relativos}
\begin{itemize}
	\item \textbf{Organização}: registros são armazenados em posições fixas, numeradas sequencialmente, como se fosse um vetor.
	\item \textbf{Acesso}: pode ser sequencial ou direto (usando um número relativo como chave).
	\item \textbf{Chave}: posição do registro no arquivo funciona como chave (chave relativa).
	\item \textbf{Uso típico}: quando você precisa acessar registros rapidamente por número de posição, como em tabelas que não mudam muito de tamanho.
\end{itemize}
São as seguintes vantagens: \vspace{-1em}
\begin{itemize}
	\item Acesso direto rápido se a chave relativa for conhecida.
	\item Mais simples de implementar que arquivos indexados.
\end{itemize}
Com as seguintes desvantagens: \vspace{-1em}
\begin{itemize}
	\item Não permite chaves alfanuméricas, apenas números.
	\item Pode ter fragmentação se registros forem apagados.
\end{itemize}

\subsection{Arquivos Indexados}
\begin{itemize}
	\item \textbf{Organização}: registros são organizados em uma estrutura que mantém um índice associado.
	\item \textbf{Acesso}: pode ser sequencial, aleatório (direto) ou dinâmico.
	\item \textbf{Chave}: permite chaves primárias e secundárias (alfa ou numéricas), como em um banco de dados.
	\item \textbf{Uso típico}: ideal para arquivos onde registros são frequentemente buscados por valores de chave (ex: CPF, matrícula, código de produto).
\end{itemize}
São as seguintes vantagens: \vspace{-1em}
\begin{itemize}
	\item Permite chaves alfanuméricas.
	\item Mais eficiente quando há muitas buscas não sequenciais.
	\item Permite chaves secundárias para buscas alternativas.
\end{itemize}
Com as seguintes desvantagens: \vspace{-1em}
\begin{itemize}
	\item Pode ser mais lento para inserção do que arquivos relativos.
	\item Requer mais espaço devido aos índices armazenados.
\end{itemize}

\subsection{Resumidamente}
Deste modo, podemos tomar uma decisão de qual forma usar:
\begin{table}[h]
	\centering
	\renewcommand{\arraystretch}{1.3} % Ajusta o espaçamento entre linhas
	\begin{tabular}{|l|l|l|}
		\hline
		\textbf{Característica} & \textbf{Arquivo Relativo} & \textbf{Arquivo Indexado} \\
		\hline
		Organização & Registros numerados sequencialmente & Registros com índice \\
		\hline
		Acesso & Sequencial ou direto (via número relativo) & Sequencial, direto ou dinâmico \\
		\hline
		Tipo de Chave & Apenas número relativo & Qualquer campo (alfa ou numérico) \\
		\hline
		Flexibilidade & Menos flexível (depende da posição) & Mais flexível (busca por diferentes chaves) \\
		\hline
		Uso Típico & Arquivos fixos, tabelas pequenas & Arquivos grandes com muitas pesquisas \\
		\hline
	\end{tabular}
	\caption{Comparação entre Arquivos Relativos e Indexados no COBOL}
	\label{tab:comparacao-arquivos}
\end{table}
